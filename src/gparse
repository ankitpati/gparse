#!/usr/bin/env perl

use Mojolicious::Lite;

plugin Config => { file => 'gparse.conf' };

use Mojo::File qw(path);
use Unicode::UTF8 qw(decode_utf8);
use HTML::Packer;

use lib path(__FILE__)->sibling('lib/perl5')->to_string;

use GParse::Domain qw(all_as_hash);

my $packer = HTML::Packer->init;
my $packer_opts = {
    remove_comments => 1,
    remove_newlines => 1,
    do_javascript => 'best',
    do_stylesheet => 'minify',
    html5 => 1,
};

{
    # Clean unnecessary routes.
    my $extra = app->static->extra;
    delete $extra->{$_} foreach keys %$extra;
}

app->renderer->add_handler (chars => sub {
    my ($renderer, $c, $output, $options) = @_;

    my $path = $renderer->template_path ($options)
        or die "$options->{template}.$options->{format}.$options->{handler}".
               ' not found on template path on the filesystem';

    my $cache = $renderer->cache;
    my $content = $cache->get ($path);
    unless (defined $content) {
        $content = decode_utf8 path($path)->slurp;
        $cache->set ($path => $content);
    }

    $$output = $content;
});

hook after_render => sub {
    my ($c, $output, $format) = @_;
    $packer->minify ($output, $packer_opts) if $format eq 'html';
};

app->renderer->compress (1);

get '/*url' => sub {
    my $c = shift;

    my $url = $c->stash ('url');

    # Stringify query, as it is not picked up in `$url` on unencoded `GET`s.
    my $query = $c->req->params->to_string;
    $url .= "?$query" if $query;

    my %response = all_as_hash $url;

    # Convert Perl truthy/falsy values to JS booleans. Refer to `Mojo::JSON`.
    foreach (keys %response) {
        $response{$_} //= '';
        $response{$_} = $response{$_} ? \1 : \0 if /^is_\w+/;
    }

    $c->render (json => \%response);
};

get '/' => 'gparse';

app->start;

__END__
